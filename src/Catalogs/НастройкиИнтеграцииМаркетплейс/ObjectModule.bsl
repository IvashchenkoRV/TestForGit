#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	Если ДанныеЗаполнения = Неопределено Тогда
		ДанныеЗаполнения = Новый Структура;
	КонецЕсли;
	
	Если НЕ ДанныеЗаполнения.Свойство("Владелец") Тогда
		Владелец = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	Иначе
		Владелец = ДанныеЗаполнения.Владелец;
	КонецЕсли;
	
	Если НЕ ДанныеЗаполнения.Свойство("ВидМаркетплейса") Тогда
		ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.WILDBERRIES;
	Иначе
		ВидМаркетплейса = ДанныеЗаполнения.ВидМаркетплейса;
	КонецЕсли;
	
	Если НЕ ДанныеЗаполнения.Свойство("Контрагент") Тогда
		Контрагент = ОбменСМаркетплейс.КонтрагентМаркетплейсаПоУмолчанию(ВидМаркетплейса);
	КонецЕсли;
	
	Если НЕ ДанныеЗаполнения.Свойство("ДоговорКонтрагента") Тогда
		РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(ДоговорКонтрагента, Контрагент, Владелец,
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером));
	КонецЕсли;
	
	Наименование = ВидМаркетплейса;
	
КонецПроцедуры

Процедура ОбновитьСписокСкладов(СписокСкладов) Экспорт
	СкладыМаркетплейс.Очистить();

	Для каждого КлючИЗначение Из СписокСкладов Цикл
		СкладМаркетплейс = СкладыМаркетплейс.Добавить();
		СкладМаркетплейс.Наименование = КлючИЗначение.Представление;
		
		ЗаполнитьЗначенияСвойств(СкладМаркетплейс, КлючИЗначение.Значение);
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	ИдентификаторСклада   = Неопределено;
	ИдентификаторКабинета = Неопределено;
	НаименованиеСклада    = Неопределено;
КонецПроцедуры

#КонецОбласти


#КонецЕсли
