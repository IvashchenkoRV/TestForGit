#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОсновнаяОрганизация           = Справочники.Организации.ОрганизацияПоУмолчанию();
	ОтборОрганизация              = ОсновнаяОрганизация;
	ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
	УстановитьВосстановленныеОтборы();
	
	Список.Параметры.УстановитьЗначениеПараметра("Организация", ОтборОрганизация);
	
	Элементы.ПереданоВАренду.Видимость = ВедетсяУчетСдачиИмуществаВАренду();
	
	ОбщегоНазначенияБП.УстановитьВидимостьКолонокДополнительнойИнформации(ЭтотОбъект);
	
	ПомеченныеНаУдалениеСервер.СкрытьПомеченныеНаУдаление(ЭтотОбъект);
	
	МобильныйКлиентАдаптацияФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененаИнформацияОС" Тогда
		Элементы.Список.Обновить();
	ИначеЕсли ИмяСобытия = "Запись_НаборКонстант" Тогда
		Элементы.ПереданоВАренду.Видимость = ВедетсяУчетСдачиИмуществаВАренду();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборОрганизацияОчистка(Элемент, СтандартнаяОбработка)
	
	ОтборОрганизацияПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	
	ОтборОрганизацияПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияИспользованиеПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
	
КонецПроцедуры

&НаСервере
Процедура ОтборОрганизацияПриИзмененииСервер()
	
	ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
	
	УстановитьВосстановленныеОтборы();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВосстановленныеОтборы()
	
	ОтборыСписковКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Организация");
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если ОтборОрганизация <> ОсновнаяОрганизация Тогда
		ОтборОрганизация = ОсновнаяОрганизация;
		ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
	ИначеЕсли НЕ ОтборОрганизацияИспользование Тогда
		ОтборОрганизацияИспользование = ЗначениеЗаполнено(ОтборОрганизация);
	КонецЕсли;
	
	УстановитьВосстановленныеОтборы();
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	
	ПомеченныеНаУдалениеКлиент.ПриИзмененииСписка(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки, ИспользуютсяСтандартныеНастройки)
	
	ПомеченныеНаУдалениеСервер.УдалитьОтборПометкаУдаления(Настройки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ВедетсяУчетСдачиИмуществаВАренду()
	
	Возврат Константы.ВедетсяУчетСдачиИмуществаВАренду.Получить();
	
КонецФункции

#КонецОбласти

#Область МобильныйКлиент

&НаСервере
Процедура МобильныйКлиентАдаптацияФормы()
	
	Если Не ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Возврат;
	КонецЕсли;
	
	МобильныйКлиентБПФормаСписка.НастроитьФормуОбъекта(ЭтотОбъект);
	
	Предупреждение = МобильныйКлиентБПФормаОбъекта.СоздатьПредупреждениеОбОграничениях(ЭтотОбъект,
		"Ввести сведения об основных средствах предприятия можно в версии для компьютера");
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКШапка(ЭтотОбъект, Предупреждение);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКСписок(ЭтотОбъект, Элементы.Список);
	
	АдаптироватьМККоманднаяПанельФормы();
	
	АдаптироватьМКОтборыФормы();
	
	АдаптироватьМКСписокФормы();
	
	АдаптироватьМККонтекстСписокФормы();
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККоманднаяПанельФормы()
	
	КомандыПоля = МобильныйКлиентБПФормаСписка.НовыйОписаниеКомандТабличноеПоле();
	КомандыПоля.ДоступноУправлениеОтбором = Ложь;
	КомандыПоля.РазрешеноИзменение = Ложь;
	МобильныйКлиентБПФормаСписка.АдаптироватьМККоманднаяПанельФормы(ЭтотОбъект, КомандыПоля);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКПерваяКомандаПанели(ЭтотОбъект, Элементы.ПодменюОтчеты);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомандаМКПоказатьСкрытьПрочиеКолонки(Команда)
	
	МобильныйКлиентБПФормаОбъектаКлиентСервер.УправлениеФормойСпискаКолонкаПрочее(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомандаМКПоказатьСкрытьОтбор(Команда)
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКОтборыФормы()
	
	МобильныйКлиентБПФормаСписка.НастроитьФормуОтбораМК(ЭтотОбъект, "Список", "Организация");
	МобильныйКлиентБПФормаСписка.АдаптироватьМКЭлементОтбораОрганизация(ЭтотОбъект, Элементы.ОтборОрганизация, "", "Все организации");
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМКБыстрыеОтборы(ЭтотОбъект, Элементы.СписокКомпоновщикНастроекПользовательскиеНастройки);

КонецПроцедуры

&НаСервере
Процедура АдаптироватьМККонтекстСписокФормы()

	МобильныйКлиентБПФормаОбъекта.АдаптироватьКонтекстноеМеню(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура АдаптироватьМКСписокФормы()
	
	МобильныйКлиентБПФормаСписка.ПодготовитьСписокФормыСправочника(ЭтотОбъект);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Важное(ЭтотОбъект, Элементы.ИнвентарныйНомер);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Важное(ЭтотОбъект, Элементы.Наименование);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Строка(ЭтотОбъект, Элементы.ГруппаСроки, 1);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Строка(ЭтотОбъект, Элементы.Местонахождение, 2);
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементВГруппуМК_Строка(ЭтотОбъект, Элементы.Ответственный, 3);
	
	МобильныйКлиентБПФормаСписка.ВстроитьЭлементыВГруппуМК_Прочее(ЭтотОбъект, "Список");
	
	Элементы.ПринятоКУчету.Формат = "ДФ='''Принято:'' dd.MM.yyyy'";
	Элементы.СнятоСУчета.Формат = "ДФ='''Снято:'' dd.MM.yyyy'";
	
	Элементы.ПереданоВАренду.Вид = ВидПоляФормы.ПолеНадписи;
	Элементы.ПереданоВАренду.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Лево;
	Элементы.ПереданоВАренду.ЦветТекста = ЦветаСтиля.ЦветТекстаСтрокаСпискаМК;
	Элементы.ПереданоВАренду.Формат = "БЛ=''; БИ='В аренде'";
	
КонецПроцедуры

#КонецОбласти