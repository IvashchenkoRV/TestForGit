
#Область СлужебныйПрограммныйИнтерфейс

// Открывает подчиненную форму в уже открытом окне.
//  Предварительно ищет уже открытую форму по переданному имени, и если такая есть, то активизирует ее.
//
// Параметры:
//  ИмяОткрываемойФормы  - Строка - полное имя открываемой формы 
//  Форма  - ФормаКлиентскогоПриложения  - форма, в окне которой требуется открыть подчиненную форму
//  ПараметрыФормы - Структура - параметры формы
//  ИскатьОткрытуюФорму - Булево - флаг для возможности отключения поиска уже открытой формы.
//                                 Может пригодиться, если открываются одинаковые формы с разными параметрами
//                                 или если имя формы переопределяется в обработке получения формы
//
Процедура ОткрытьФормуВОкнеФормы(ИмяОткрываемойФормы, Форма, ПараметрыФормы = Неопределено, ИскатьОткрытуюФорму = Истина) Экспорт
	
	//В мобильном клиенте все формы открываются в одном окне-экране.
	//Вернуться в родительскую форму из подчиненной невозможно.
	Если МобильныйКлиентБПКлиент.ЭтоМобильныйКлиент() Тогда
		ОткрытьФорму(ИмяОткрываемойФормы, ПараметрыФормы, Форма);
		Возврат;
	КонецЕсли;
	
	// При открытии форм внутри существующего окна ключ уникальности не используется для поиска уже открытой
	// подчиненной формы, поэтому перед окрытием проверяем наличие открытой ранее формы кодом
	// Браузеры блокируют активизацию форм, чтобы их не плодить закрываем существующую перед открытием новой
	Если ИскатьОткрытуюФорму Тогда
		Для Каждого ПодчиненнаяФорма Из Форма.Окно.Содержимое Цикл
			Если ПодчиненнаяФорма.ИмяФормы = ИмяОткрываемойФормы Тогда
				#Если ВебКлиент Тогда
					ПодчиненнаяФорма.Закрыть();
				#Иначе
					ПодчиненнаяФорма.Активизировать();
					Возврат;
				#КонецЕсли
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ОткрытьФорму(ИмяОткрываемойФормы, ПараметрыФормы, Форма, , Форма.Окно);
	
КонецПроцедуры

#КонецОбласти