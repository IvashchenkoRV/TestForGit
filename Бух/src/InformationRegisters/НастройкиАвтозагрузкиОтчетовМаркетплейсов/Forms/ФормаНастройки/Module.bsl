
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьНастройки();
	
	Если Параметры.Свойство("ВключитьАвтозагрузку") И Не АвтозагрузкаВключена Тогда
		АвтозагрузкаВключена = Параметры.ВключитьАвтозагрузку;
		Модифицированность = Истина;
	КонецЕсли;
	
	УправлениеВидимостью();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаписьНастройкаИнтеграцииМаркетплейс" Тогда
		
		ЗаполнитьНастройки();
		УправлениеВидимостью();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если НЕ ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		ВопросОСохраненииНастроек(Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура АвтозагрузкаВключенаПриИзменении(Элемент)
	
	УправлениеВидимостью();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеньЗагрузкиЕжемесячныхОтчетовПриИзменении(Элемент)
	
	УправлениеВидимостью();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеньЗагрузкиЕженедельныхОтчетовПриИзменении(Элемент)
	
	УправлениеВидимостью();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НастройкиИнтеграции(Команда)
	
	ОткрытьФорму("Справочник.НастройкиИнтеграцииМаркетплейс.ФормаСписка",, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьНастройки(Команда)
	
	Если Не Модифицированность Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьНастройки();
	Оповестить("ПараметрыАвтозагрузкиОтчетовМаркетплейсовИзменены");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеВидимостью()
	
	НастройкиАвтозагрузки = РегистрыСведений.НастройкиАвтозагрузкиОтчетовМаркетплейсов;
	
	Если АвтозагрузкаВключена Тогда
		Элементы.АвтозагрузкаВключена.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	Иначе
		Элементы.АвтозагрузкаВключена.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
	КонецЕсли;
	
	Элементы.ГруппаПараметры.Видимость = АвтозагрузкаВключена;
	Элементы.КоманднаяПанельЛевая.Видимость = АвтозагрузкаВключена;
	
	Элементы.ГруппаЕженедельныеОтчеты.Видимость = ЕстьЕженедельныеОтчеты;
	Элементы.ГруппаЕжемесячныеОтчеты.Видимость = ЕстьЕжемесячныеОтчеты;
	
	Элементы.ЕжемесячныеОтчетыСледующаяЗагрузка.Заголовок =
		НастройкиАвтозагрузки.ПредставлениеСледующейЗагрузки(Перечисления.ПериодичностьОтчетовМаркетплейсов.Месяц, ДеньЗагрузкиЕжемесячныхОтчетов);
	
	Элементы.ЕженедельныеОтчетыСледующаяЗагрузка.Заголовок =
		НастройкиАвтозагрузки.ПредставлениеСледующейЗагрузки(Перечисления.ПериодичностьОтчетовМаркетплейсов.Неделя, ДеньЗагрузкиЕженедельныхОтчетов);
	
КонецПроцедуры

#Область ЧтениеЗаписьНастроек

&НаСервере
Процедура ЗаполнитьНастройки()
	
	ПериодичностьОтчетов = ОбменСМаркетплейс.ПериодичностьОтчетовМаркетплейсов();
	ЕстьЕжемесячныеОтчеты = ПериодичностьОтчетов.Ежемесячно;
	ЕстьЕженедельныеОтчеты = ПериодичностьОтчетов.Еженедельно;
	МаркетплейсыЕжемесячно = ПериодичностьОтчетов.МаркетплейсыЕжемесячно;
	МаркетплейсыЕженедельно = ПериодичностьОтчетов.МаркетплейсыЕженедельно;
	
	НастройкиАвтозагрузки = РегистрыСведений.НастройкиАвтозагрузкиОтчетовМаркетплейсов;
	
	АвтозагрузкаВключена = НастройкиАвтозагрузки.АвтозагрузкаВключена();
	
	Периодичность = Перечисления.ПериодичностьОтчетовМаркетплейсов.Неделя;
	ДеньЗагрузкиПоУмолчанию = НастройкиАвтозагрузки.ДеньЗагрузкиПоУмолчанию(Периодичность);
	ДеньЗагрузкиЕженедельныхОтчетов = НастройкиАвтозагрузки.ЗначениеПараметраАвтозагрузки(Периодичность, "ДеньЗагрузки", ДеньЗагрузкиПоУмолчанию);
	
	Периодичность = Перечисления.ПериодичностьОтчетовМаркетплейсов.Месяц;
	ДеньЗагрузкиПоУмолчанию = НастройкиАвтозагрузки.ДеньЗагрузкиПоУмолчанию(Периодичность);
	ДеньЗагрузкиЕжемесячныхОтчетов = НастройкиАвтозагрузки.ЗначениеПараметраАвтозагрузки(Периодичность, "ДеньЗагрузки", ДеньЗагрузкиПоУмолчанию);
	
	Элементы.ЗаголовокЕжемесячныеОтчетыДеньЗагрузки.Заголовок =
		СтрШаблон(НСтр("ru = 'Загружать отчеты %1 с'"),
		МаркетплейсыЕжемесячно);
	
	Элементы.ЗаголовокЕженедельныеОтчетыДеньЗагрузки.Заголовок =
		СтрШаблон(НСтр("ru = 'Загружать отчеты %1 по'"),
		МаркетплейсыЕженедельно);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросОСохраненииНастроек(ЗакрытиеФормы = Ложь)
	
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить(КодВозвратаДиалога.Да, "Да");
	Кнопки.Добавить(КодВозвратаДиалога.Отмена, "Отмена");
	
	ТекстВопроса = НСтр("ru = 'Настройки не были сохранены.
		|Продолжить без сохранения?'");
	
	Оповещение = Новый ОписаниеОповещения("ВопросОСохраненииНастроекПродолжение", ЭтотОбъект, ЗакрытиеФормы);
	ПоказатьВопрос(Оповещение, ТекстВопроса, Кнопки, , КодВозвратаДиалога.Отмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросОСохраненииНастроекПродолжение(Ответ, ЗакрытиеФормы) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Ложь;
	
	Если ЗакрытиеФормы Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройки()
	
	НастройкиАвтозагрузки = РегистрыСведений.НастройкиАвтозагрузкиОтчетовМаркетплейсов;
	
	Если АвтозагрузкаВключена Тогда
		Если ЕстьЕженедельныеОтчеты Тогда
			НовыеНастройки = НастройкиАвтозагрузки.НовыеПараметрыНастройки();
			НовыеНастройки.ДеньЗагрузки = ДеньЗагрузкиЕженедельныхОтчетов;
			НовыеНастройки.ИдентификаторЗадания = НастройкиАвтозагрузки.ИдентификаторЗадания(Перечисления.ПериодичностьОтчетовМаркетплейсов.Неделя);
			НастройкиАвтозагрузки.СохранитьНастройки(Перечисления.ПериодичностьОтчетовМаркетплейсов.Неделя, НовыеНастройки);
		Иначе
			НастройкиАвтозагрузки.СохранитьНастройки(Перечисления.ПериодичностьОтчетовМаркетплейсов.Неделя);
		КонецЕсли;
		Если ЕстьЕжемесячныеОтчеты Тогда
			НовыеНастройки = НастройкиАвтозагрузки.НовыеПараметрыНастройки();
			НовыеНастройки.ИдентификаторЗадания = НастройкиАвтозагрузки.ИдентификаторЗадания(Перечисления.ПериодичностьОтчетовМаркетплейсов.Месяц);
			НовыеНастройки.ДеньЗагрузки = ДеньЗагрузкиЕжемесячныхОтчетов;
			НастройкиАвтозагрузки.СохранитьНастройки(Перечисления.ПериодичностьОтчетовМаркетплейсов.Месяц, НовыеНастройки);
		Иначе
			НастройкиАвтозагрузки.СохранитьНастройки(Перечисления.ПериодичностьОтчетовМаркетплейсов.Месяц);
		КонецЕсли;
	Иначе
		НастройкиАвтозагрузки.СохранитьНастройки(Перечисления.ПериодичностьОтчетовМаркетплейсов.Неделя);
		НастройкиАвтозагрузки.СохранитьНастройки(Перечисления.ПериодичностьОтчетовМаркетплейсов.Месяц);
	КонецЕсли;
	
	Модифицированность = Ложь;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти